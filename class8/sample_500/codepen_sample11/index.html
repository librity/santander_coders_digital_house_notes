<!-- https://codepen.io/shubniggurath/pen/NLYzLj -->

<script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/88/three.min.js"></script>
<script id="vertexShader" type="x-shader/x-vertex">
  void main() {
      gl_Position = vec4( position, 1.0 );
  }
</script>
<script id="fragmentShader" type="x-shader/x-fragment">
  uniform vec2 u_resolution;
  uniform vec2 u_mouse;
  uniform float u_time;
  uniform sampler2D u_noise;
  uniform sampler2D u_500;

  #define PI 3.141592653589793
  #define TAU 6.283185307179586
  #define pow2(x) (x * x)

  // blur constants
  const int samples = 4;
  const float sigma = float(samples) * 0.25;

  float gaussian(vec2 i) {
    return 1.0 / (2.0 * PI * pow2(sigma)) * exp(-((pow2(i.x) + pow2(i.y)) / (2.0 * pow2(sigma))));
  }

  vec3 blur(sampler2D sp, vec2 uv, vec2 scale) {
    vec3 col = vec3(0.0);
    float accum = 0.0;
    float weight;
    vec2 offset;

    for (int x = -samples / 2; x < samples / 2; ++x) {
        for (int y = -samples / 2; y < samples / 2; ++y) {
            offset = vec2(x, y);
            weight = gaussian(offset);
            col += texture2D(sp, uv + scale * offset).rgb * weight;
            accum += weight;
        }
    }

    return col / accum;
  }

  vec2 hash2(vec2 p)
  {
    vec2 o = texture2D( u_noise, (p+0.5)/256.0, -100.0 ).xy;
    return o;
  }

  vec3 hsb2rgb( in vec3 c ){
    vec3 rgb = clamp(abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),
                             6.0)-3.0)-1.0,
                     0.0,
                     1.0 );
    rgb = rgb*rgb*(3.0-2.0*rgb);
    return c.z * mix( vec3(1.0), rgb, c.y);
  }

  vec3 domain(vec2 z){
    return vec3(hsb2rgb(vec3(atan(z.y,z.x)/TAU,1.,1.)));
  }
  vec3 colour(vec2 z) {
      return domain(z);
  }

  const int layers = 9;
  const float zoomSpeed = -4.;

  void main() {
    vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution.xy) / min(u_resolution.y, u_resolution.x);

    vec3 fragcolour = vec3(0.);
    float opacity = 0.;

    for(int i = 0; i < layers; i++) {
      float layer = float(i);
      // float scale = mod(u_time / float(layers) * layer, 1.);
      float factor = (u_time + zoomSpeed / float(layers) * float(layer)) / zoomSpeed;
      float scale = mod(factor, 1.);
      float s = sin(scale*2.-.3);
      float c = cos(scale*2.);
      vec2 _uv = uv * 4. * scale * mat2(c, -s, s, c) * (1. + cos(uv.y*5.)*.1) * (1. + sin(uv.x*.5)*.1);
      // endOpacity = smoothstep(0., 0.2, scale * -1.);
      float _opacity = (1. - scale) * smoothstep(0., 0.3, scale);
      opacity += _opacity;
      // float mask = (1. - texture2D(u_500, _uv - .5).r) * _opacity;
      float mask = (1. - blur(u_500, _uv - .5, vec2(max(0., 0.01 * (.5-scale)))).r) * _opacity;
      fragcolour += mask*mask*4.;
    }

    fragcolour /= opacity;

    gl_FragColor = vec4(fragcolour,1.0);
  }
</script>

<div id="container" touch-action="none"></div>
<div id="description">
  <p>
    Oops. Looks like it's broken. While we go down this rabbit hole, please go
    outside and enjoy the sunshine.
  </p>
  <p>
    If you continue to see this error, please contact liam@wethecollective.com
  </p>
</div>
